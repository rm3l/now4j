//Copyright (c) 2017 Armel Soro
//
//Permission is hereby granted, free of charge, to any person obtaining a copy
//of this software and associated documentation files (the "Software"), to deal
//in the Software without restriction, including without limitation the rights
//to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
//copies of the Software, and to permit persons to whom the Software is
//furnished to do so, subject to the following conditions:
//The above copyright notice and this permission notice shall be included in all
//copies or substantial portions of the Software.
//
//THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
//IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
//FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
//AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
//LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
//OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
//SOFTWARE.
import org.slf4j.Logger
import org.slf4j.LoggerFactory

Logger logger = LoggerFactory.getLogger('now4j-parent')

def artifactGroupId = 'org.rm3l'
def artifactVersion = '1.0.0'

buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.6'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

subprojects {
    group artifactGroupId
    version artifactVersion

    apply plugin: 'java'
    apply plugin: 'com.jfrog.bintray'
    apply plugin: 'com.github.dcendents.android-maven'

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    repositories {
        mavenCentral()
    }

    dependencies {
        testCompile group: 'junit', name: 'junit', version: '4.12'
        compile 'org.jetbrains:annotations:17.0.0'
    }

    javadoc {
        failOnError  false
        source = sourceSets.main.java.srcDirs
        classpath += configurations.compile
    }

    // build a jar with source files
    task sourcesJar(type: Jar) {
        from sourceSets.main.java.srcDirs
        classifier = 'sources'
    }

    // build a jar with javadoc
    task javadocJar(type: Jar, dependsOn: javadoc) {
        classifier = 'javadoc'
        from javadoc.destinationDir
    }

    artifacts {
        archives sourcesJar
        archives javadocJar
    }
}

//For Bintray publishing signing
def homePath = System.properties['user.home'].toString()

//Attempt to read properties file for deployment on Bintray, if any
final propsFile = new File(homePath, ".bintray.properties")

def bintrayUser = ""
def bintrayKey = ""
if (propsFile.exists()) {
    logger.debug("[Bintray] Using properties file located at ${propsFile.absolutePath}")
    final props = new Properties()
    props.load(new FileInputStream(propsFile))
    bintrayUser = props['user']
    bintrayKey = props['key']
}

ext.getBintrayConfig = {
    artifactId, artifactLabels, description ->
        logger.debug("[Bintray] getBintrayConfig($artifactId, $artifactLabels, $description)")
        return {
            user = bintrayUser
            key = bintrayKey

            publish = true //If version should be auto published after an upload

            pkg {
                repo = 'maven'
                name = "${artifactGroupId}:${artifactId}"
                labels = artifactLabels

                //noinspection GroovyAssignabilityCheck
                version {
                    name = artifactVersion
                    desc = description
                    released = new Date()
                    vcsTag = artifactVersion
                }

                publicDownloadNumbers = true

                licenses = ['MIT']
                vcsUrl = 'https://github.com/rm3l/now4j.git'
                websiteUrl = 'https://github.com/rm3l/now4j'
                issueTrackerUrl = 'https://github.com/rm3l/now4j/issues'

            }
            configurations = ['archives']
        }
}